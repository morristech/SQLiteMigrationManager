apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'git-repo'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 14
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    androidTestCompile fileTree(dir: 'src/androidTest/libs', include: ['*.jar'])
    androidTestCompile 'com.squareup:fest-android:1.0.7'

}

/*************************************************
 * Uploading
 */

task bundleJar(type: Jar) {
    from fileTree(dir: 'build/intermediates/classes/release').matching {
        include 'com/layer/sqlite/**'
    }
}

task sourceJar(type: Jar) {
    from fileTree(dir: 'src/main')
}

project.ext {
    gitRepoHome = project.hasProperty("gitRepoHome") ? project.getProperty("gitRepoHome") : "${System.env.HOME}/.gitRepos"
    org = project.hasProperty("org") ? project.getProperty("org") : "layerhq"
    repo = project.hasProperty("repo") ? project.getProperty("repo") : "maven-private"
}

public String getLocalGit() {
    return "$project.ext.gitRepoHome/$project.ext.org/$project.ext.repo"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "com.layer.sqlite"
            artifactId "migrationmanager"
            artifact bundleJar

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file://${getLocalGit()}/releases"
        }
    }
}

task cleanLocalGit(type: Delete) {
    doFirst {
        println "Deleting local git repo `${getLocalGit()}`..."
        delete "${getLocalGit()}"
    }
}

